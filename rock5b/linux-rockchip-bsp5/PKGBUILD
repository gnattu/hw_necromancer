# Maintainer: Faruk Dikcizgi <boogiepop@gmx.de>
# Contributor: Jan Keith Darunday <aur@jkcdarunday.mozmail.com>
# Contributor: Jack Chen <redchenjs@live.com>

pkgver=5.10.110
_commit=gca15bbe36e6c
_patchlevel=34
_kernver="${pkgver}-${_patchlevel}-rockchip-${_commit}"
pkgbase=linux-rockchip-bsp5
pkgname=("$pkgbase" "$pkgbase-headers")
pkgrel=1
arch=('aarch64')
url="https://github.com/radxa/kernel"
_repourl="https://github.com/radxa/apt/raw/gh-pages/bullseye-stable/pool/main/"
license=('GPL2')
options=('!strip')
makedepends=('binutils' 'tar' 'grep' 'mkinitcpio' 'sed')
pkgdesc="Precompiled Kernel Images based on 5.10.x BSP kernel published by RADXA"

source=(
    "linux.preset"
    "extract-vmlinux"
    "${_repourl}/l/linux-${_kernver}/linux-headers-${_kernver}_${pkgver}-${_patchlevel}-rockchip_arm64.deb"
    "${_repourl}/l/linux-${_kernver}/linux-image-${_kernver}_${pkgver}-${_patchlevel}-rockchip_arm64.deb"
    )
sha512sums=(
    'f01e7925b262d2874a8a991b1f27d057356a2a384d2012b61be5a631d4e4d7cf87461c8fb9e7f183831f5a829ad204897f1f0545a52df6288a0e04a5c2e31b96'
    'ba6edcd5f56a4c3e865578eabf1e1311b8e5e7babffdb0f84b99971238505ca27eba506f2723638129ef6558eba95f7e4fd2e07b40e5a4de716e5de90d1cbe79'
    '5b335a98939d9d2adbfe9ec55342af151066919d6504aecbd165e08e437b133d1bf311962321195a05093cd05197ba41bf2aa8c17355dab8401e84cb067b0c7b'
    '1327ef88ff29cb621c6b95a4677a9a107816c369ad132c07dc4a8de40322aec31e3920f37a3b92abeb64d0a4bad4900cfe16cfc68546039ed919222934615ee6'
    )
noextract=("${source[@]##*/}")

prepare() {
  cd "$srcdir"

  rm -rf $(find -mindepth 1 -maxdepth 1 -type d)
}

_package() {
  pkgdesc="The Linux 5.x Kernel and Modules for Rockchip published by Radxa"
  depends=('coreutils' 'linux-firmware' 'kmod' 'mkinitcpio>=0.7' 'grep' 'gzip')
  optdepends=('crda: to set the correct wireless channels of your country')
  backup=("etc/mkinitcpio.d/$pkgbase.preset")
  provides=("linux" "linux-rockchip-bsp" "linux-rockchip-bsp5")
  conflicts=("linux-rockchip-bsp-5")

  cd "$srcdir"

  # extract image deb
  ar x "linux-image-${_kernver}_${pkgver}-${_patchlevel}-rockchip_arm64.deb"
  tar -m -xf data.tar.xz
  install -dm755 "$pkgdir/boot"
  install -dm755 "$pkgdir/usr"
  
  # copy dtbs
  mkdir "$pkgdir/boot/dtbs"
  cp -rf "usr/lib/linux-image-${_kernver}/rockchip/" "$pkgdir/boot/dtbs/rockchip"

  # copy modules
  cp -r lib "$pkgdir/usr/lib"

  # copy kernel, bootlaoder can not load compressed kernels
  ./extract-vmlinux "boot/vmlinuz-${_kernver}" > "$pkgdir/usr/lib/modules/$_kernver/vmlinuz"

  # sed expression for following substitutions
  local _subst="
    s|%PKGBASE%|$pkgbase|g
    s|%KERNVER%|$_kernver|g
  "

  # install mkinitcpio preset file
  sed "$_subst" linux.preset |
    install -Dm644 /dev/stdin "$pkgdir/etc/mkinitcpio.d/$pkgbase.preset"

  # used by mkinitcpio to name the kernel
  echo "$pkgbase" | install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules/$_kernver/pkgbase"
}

_package-headers() {
  pkgdesc="The Linux 5.x Kernelmakepkg  Headers for Rockchip published by Radxa"
  provides=("linux-headers")
  conflicts=("linux-headers")

  cd "$srcdir"

  # extract header deb
  ar x "linux-headers-${_kernver}_${pkgver}-${_patchlevel}-rockchip_arm64.deb"
  tar -m -xf data.tar.xz
  install -dm755 "$pkgdir/usr/lib/modules/$_kernver"

  # copy headers
  cp -r "usr/src/linux-headers-$_kernver" "$pkgdir/usr/lib/modules/$_kernver/build"

  # add real version for building modules and running depmod from hook
  echo "$_kernver" |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules/$_kernver/build/version"
}

for _p in "${pkgname[@]}"; do
  eval "package_$_p() {
    $(declare -f "_package${_p#$pkgbase}")
    _package${_p#$pkgbase}
  }"
done
